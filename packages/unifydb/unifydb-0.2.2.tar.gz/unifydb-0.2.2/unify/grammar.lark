start: command+ ";"?

command: show_tables
         | show_schemas
         | show_variables
         | show_columns
         | show_connections
         | select_for_writing
         | select_query
         | create_statement
         | create_view_statement
         | insert_statement
         | delete_connection
         | delete_statement
         | drop_table
         | drop_schema
         | alter_table
         | describe
         | clear_table
         | refresh_table
         | reload_table
         | peek_table
         | create_chart
         | count_table
         | export_table
         | set_variable
         | show_variable
         | email_command
         | run_notebook_command
         | run_schedule
         | run_info
         | delete_schedule
         | help
         | show_files
         | import_command
         | search_command
         | help_last
         | connect_command
         | system_command
         | open_command

show_tables: "show tables" (WS FROM WS? schema_ref?)?

show_schemas: "show" WS "schemas"

show_variables: "show" WS "variables"

show_files: "show" WS "files" (WS "from" WS schema_ref)? (WS "like" WS match_expr)?

match_expr: "'" PATTERN "'"

show_columns: "show columns" (WS FROM WS table_ref WS? column_filter?)?

column_filter: "like" WS "'" PATTERN "'"

show_connections: "show connections"

select_query: "select"i WS col_list WS "from"i WS table_list (WS where_clause)? (WS order_clause)? (WS limit_clause)?

select_for_writing: select_query ">>" adapter_ref file_ref writing_args?

adapter_ref: DOTTED_WORD | SINGLE_QUOTED_STRING
file_ref: SINGLE_QUOTED_STRING | PAREN_EXPR
writing_args: ANY

export_table: "export" WS table_ref WS "to" adapter_ref file_ref? (write_option)?

write_option: OVERWRITE | APPEND
OVERWRITE: "overwrite"
APPEND: "append"

set_variable: var_ref WS? "=" var_expression

show_variable: var_ref

var_expression: ANY

describe: "describe" table_ref?

create_statement: "create table" ANY

create_view_statement: "create view" ANY

insert_statement: "insert" ANY

delete_connection: "delete connection"

delete_statement: "delete" ANY

drop_table: "drop" WS "table" WS ("if exists"i WS)? table_ref

drop_schema: "drop schema" WS schema_ref (WS "cascade")?

alter_table: "alter"i WS "table"i WS table_ref "rename"i WS "to" WS new_table

new_table: table_ref

clear_table: "clear table" WS table_schema_ref

refresh_table: "refresh table" WS table_ref

reload_table: "reload table" WS table_ref

peek_table: "peek" WS qualifier? WS? peek_object line_count?

peek_object: table_ref | SINGLE_QUOTED_STRING | URL

qualifier: ("at" | "file")

line_count: NUMBER

email_command: "email" WS email_object (WS chart_ref)? WS "to" recipients WS? ("subject" WS subject)?

email_object: "notebook"|"chart"|table_ref

run_notebook_command: run_at_command 
             | run_every_command 

run_at_command: "run" WS (notebook_ref WS)? "at" WS datetime
run_every_command: "run" WS (notebook_ref WS)? "every" WS repeater WS "starting"? WS "at" WS datetime

run_schedule: "run" WS "schedule"

run_info: "run" WS "info" schedule_ref

delete_schedule: "run" WS "delete" schedule_ref

import_command: "import" WS file_path (options)?

options: ANY

search_command: "search"i WS query

query: ANY

help_last: "??"

connect_command: "connect"

system_command: "system" WS sys_command

open_command: "open" WS open_target

open_target: ANY

sys_command: "status" | "stop daemon" | "restart daemon" | "cancel"

file_path: SINGLE_QUOTED_STRING | /\S+/

schedule_ref: SINGLE_QUOTED_STRING

notebook_ref: SINGLE_QUOTED_STRING
repeater: DAY | WEEK | WEEKDAY | MONTH
DAY: "day"
WEEK: "week"
WEEKDAY: "weekday"
MONTH: "month"

datetime: (DATE " ")? TIME
DATE: DIGIT~4 "-" DIGIT~2 "-" DIGIT~2
TIME: DIGIT~2 ":" DIGIT~2

chart_ref: SINGLE_QUOTED_STRING

recipients: SINGLE_QUOTED_STRING

subject: SINGLE_QUOTED_STRING

help: "help" (WS HELP_CHOICE)?

HELP_CHOICE: "info" | "schemas" | "charts" | "import" | "export"

col_list: col_ref WS? ("," WS? col_ref)*

col_ref: (WORD ("." WORD)* | "*" | agg_func) (WS "as" WS WORD)?

agg_func: WORD WS* "(" /[^)]+/ ")"

table_ref: WORD ("." WORD)?
table_list: table_ref WS? ("," WS? table_ref)*
table_schema_ref: WORD "." WORD?
schema_ref: WORD
limit_clause: "limit" WS NUMBER
var_ref: "$" WORD

where_clause: "where"i WS where_comp (["and"i | "or"i] WS where_comp)*
where_comp: col_ref OPERATOR [LITERAL | col_ref]
order_clause: "order"i WS "by"i WS order_col_list
order_col_list: col_ref (WS "asc"|"desc")? WS? ("," WS? col_ref)*


TABLE: WORD
SCHEMA: WORD

create_chart_where: "where" (WS chart_param WS? "=" WS? param_value) (WS "and" WS chart_param WS? "=" WS? param_value)*
create_chart: "create chart" WS? chart_name? WS? ("from" chart_source)? ("as" chart_type WS)? create_chart_where?

chart_name: WORD
chart_param: X_PARAM
             | Y_PARAM
             | TITLE_PARAM
             | X_AXIS_LABEL
             | STACKED_PARAM
             | COLOR_PARAM
             | THETA
             | TRENDLINE
             | ROLLING
             
X_PARAM: "x"
Y_PARAM: "y"
TITLE_PARAM: "title"
X_AXIS_LABEL: "x_axis_label"
STACKED_PARAM: "stacked"
COLOR_PARAM: "color"
THETA: "theta"
TRENDLINE: "trendline"
ROLLING: "rolling"

param_value: WORD | LITERAL
column_ref: WORD
chart_source: "$"? table_ref
chart_type: PIE_CHART 
            | BAR_CHART
            | HBAR_CHART
            | LINE_CHART
            | AREA_CHART

PIE_CHART: "pie_chart"
BAR_CHART: "bar_chart"
HBAR_CHART: "hbar_chart"
LINE_CHART: "line_chart"
AREA_CHART: "area_chart" 

// SQL extensions

count_table: "count" WS table_ref

parts: WORD ","? [ WS WORD+ ]

ANY             : /.+/ [WS /.*/]*
DIGIT           : "0".."9"
LCASE_LETTER    : "a".."z"
UCASE_LETTER    : "A".."Z"
OPERATOR        : "=" | "!=" | "<" | ">" | "<>" | "is" | "not is"
LITERAL         : SINGLE_QUOTED_STRING | NUMBER 
ALPHA           : UCASE_LETTER | LCASE_LETTER | "_"
ALPHANUM        : ALPHA [ ALPHA | DIGIT ]*
MIXED_ALPHA     : (ALPHA | DIGIT)+
SINGLE_QUOTED_STRING  : /'[^']*'/
PAREN_EXPR      : "(" /[^)]+/ ")"
URL             : /http[s]?:\S+/
SPACE           : " "
TAB             : SPACE+
FROM            : "from" | "FROM"
NAME            : ALPHANUM+
WORD            : ALPHANUM+
DOTTED_WORD     : WORD ("." WORD)*
PATTERN         : /[\.\*\%A-Za-z0-9_\s]+/
NUMBER          : /-*[\d\.]+/
WS              : /[ \t\f\r\n]/+

%ignore WS
